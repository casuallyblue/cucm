plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.github.casually-blue'
version '1.0'

ext.cxfVersion = "3.4.4"

repositories {
    mavenCentral()
}

configurations {
    wsdl2java
}

dependencies {
    wsdl2java("org.apache.cxf:cxf-tools-wsdlto-core:${cxfVersion}")
    wsdl2java("org.apache.cxf:cxf-tools-wsdlto-frontend-jaxws:${cxfVersion}")
    wsdl2java("org.apache.cxf:cxf-tools-wsdlto-databinding-jaxb:${cxfVersion}")
}

task wsdl2java(type: JavaExec) {
    main = "org.apache.cxf.tools.wsdlto.WSDLToJava"
    classpath = configurations.wsdl2java
    // Uncomment to add JVM arguments if necessary (e.g. for cert-based security)
    //jvmArgs = [
    //    "-Djavax.net.ssl.keyStore=${keystorePath}",
    //    "-Djavax.net.ssl.keyStorePassword=${keystorePassword}"
    //]
    args = [
            "-d", "src/main/java",
            "src/main/resources/AXLAPI.wsdl"
    ]
}

publishing {

    repositories {
        maven {
            name = "com.github.casually-blue.cucm"
            url = "https://maven.pkg.github.com/casually-blue/com.github.casually-blue.cucm"
            credentials {
                username= project.findProperty("gpr.user")
                password= project.findProperty("gpr.key")
            }
        }
    }

    publications {
        gpr(MavenPublication){
            from(components.java)
        }
    }
}